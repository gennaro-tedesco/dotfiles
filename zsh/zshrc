if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# themes and prompt
ZSH_THEME="powerlevel10k/powerlevel10k"
POWERLEVEL9K_MODE="awesome-patched"

# list of plugins to load
plugins=(
		auto-ls
		fzf-tab
		vi-mode
		zsh-interactive-cd
		zsh-autosuggestions
		zsh-syntax-highlighting
		z
		)
unsetopt autocd

# Path to your oh-my-zsh installation.
export ZSH="${HOME}/.oh-my-zsh"
source $ZSH/oh-my-zsh.sh

# exporting environment variables
export EDITOR="nvim"
export LANG=en_US.UTF-8
export LC_CTYPE="en_US.UTF-8"
export MANPAGER="sh -c 'col -bx | bat -l man -p'"
export FZF_DEFAULT_OPTS='--reverse --border -e --height=50% --padding=1'
export FZF_DEFAULT_COMMAND="rg --files --hidden -g !.git/"

# exporting go paths
export GOPATH=$HOME/go
export GOBIN=$GOPATH/bin
export PATH=${PATH}:$GOBIN

# aliases
alias ls="exa --long --header --git --sort=extension"
alias tree="exa --tree --level=2"
alias cat="preview_files"
alias todo="${EDITOR} ~/.todo"
alias zenith="zenith -d 0 -n 0"
alias du="dust"
alias grep="rg"
alias jd="json_diff"

## ----------------
## custom functions
## ----------------

# fzf browse files
ff() (
  IFS=$'\n' files=($(fzf --preview 'bat --style=numbers --color=always {} | head -500' --query="$1" --multi --select-1 --exit-0))
  [[ -n "$files" ]] && ${EDITOR} "${files[@]}"
)


# fzf browse directories and cd into them
fd() {
  local dir
  dir=$(find ${1:-.} -type d 2> /dev/null | fzf +m) && cd "$dir"
}


# fzf checkout git branches
fb() {
  local branches branch
  branches=$(git branch --all | grep -v HEAD) &&
  branch=$(echo "$branches" |
		   fzf-tmux -d $(( 2 + $(wc -l <<< "$branches") )) +m) &&
  git checkout $(echo "$branch" | sed "s/.* //" | sed "s#remotes/[^/]*/##")
}


# list man pages
preview_man() {
  local manpage
  manpage=$(man -k . 2> /dev/null | grep -F '(1)' | sort -u | sed 's/(.*//' | fzf) && man "$manpage"
}


# preview files
preview_files () {
	if [[ $1 == *.md ]]; then
		glow -s ~/.config/glowconfig/customglow.json -p $1
	elif [[ $1 == *.json ]]; then
		jq '.' -C $1 | less
	elif [[ $1 == *.csv ]]; then
		vd $1
	else
		bat --theme='Solarized (dark)' --style='header,grid' $1
	fi
}

# json_diff
json_diff(){
	[[ $# != 2 ]] && { echo "expected two arguments"; return}
	[[ ( $1 != *.json ) || ( $2 != *.json ) ]] && { echo "arguments must be both json files"; return}
	[[ ! -f $1 ]] && { echo "cannot find $1"; return}
	[[ ! -f $2 ]] && { echo "cannot find $2"; return}

	delta <(jq -S . $1) <(jq -S . $2)
}

unalias z 2> /dev/null
z() {
  [ $# -gt 0 ] && _z "$*" && return
  cd "$(_z -l 2>&1 | fzf --height 40% --nth 2.. --reverse --inline-info +s --tac --query "${*##-* }" | sed 's/^[0-9,.]* *//')"
}

## -----------------------
## additional source files
## -----------------------

# sourcing fzf keybindings
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# navi cheat sheet (Ctrl+g)
eval "$(navi widget zsh)"


## --------
## bindkeys
## --------
bindkey -s '^p' 'ff\n'
bindkey -s '^n' 'vifm\n'
bindkey -s '^[OP' 'preview_man\n'

